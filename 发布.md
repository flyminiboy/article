## 发布

每个高尚的把程序猿都怀揣一颗拯救世界的心。希望自己的优秀代码可以发布到仓库供全世界的猿一起享用。

那么什么是**发布**呢？？？

通俗来说发布搞明白下面三个事情就可以了。

1. **What to publish**

2. **Where to publish**

3. **How to publish**

其实将项目发布为一个模块，只需要解决上面三个问题。

**What** 就是我们项目要发布的文件和信息，这些内容在 `gradle` 中被称为 **发布的工件和元数据的组合**

**Where** 就是我们要发布到的哪里，才可以让大家都享受呢，当然你可以小范围享受，我们一般称这个地点为**仓库**。比如知名仓库**maven**，**ivy**这些。

**How** 怎么发布！！！不要慌，不要慌，这种难搞的事情肯定已经有现成的。比如 [Maven Publish Plugin](https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven)，[Ivy Publish Plugin](https://docs.gradle.org/current/userguide/publishing_ivy.html#publishing_ivy)

通过一张图总结

![发布总结图](https://mmbiz.qpic.cn/mmbiz_png/ibExRe3rl9wfzyMUaEdyQOYNYpyDicLGvtc2mxTUWAz8ZFQ3vPb7ByicXqP942pW0DOHz4xf1gRBuFNiclkYj0hy5Q/0?wx_fmt=png)


我们今天只介绍通过 `Maven Publish Plugin` 这个插件将项目发布到本地。

再插入一个小话题，简单说一下 **Maven POM 文件**。

### Maven POM

POM（Project Object Model，项目对象模型 ）是 Maven 工程的基本工作单元，是一个**XML文件**，包含了项目的基本信息，用于描述项目如何构建，声明项目依赖等。

所有 POM 文件都需要 `project` 元素和三个必需字段：`groupId`，`artifactId`，`version`

|  字段   | 解释  |
|  ----  | ----  |
| groupId  | 这是工程组的标识。它在一个组织或者项目中通常是唯一的 |
| artifactId  | 项目的唯一ID，一个groupId下面可能多个项目，就是靠artifactId来区分的 |
| version | 版本号 |


举个栗子

```
implementation 'com.google.code.gson:gson:2.9.0'
```

嗯是熟悉的味道。下面让我们来庖丁解牛。

|  字段   | 示例  |
|  ----  | ----  |
| groupId  | com.google.code.gson |
| artifactId  | gson |
| version | 2.9.0 |


> POM 可以指定配置信息

* 项目依赖
* 插件
* 执行目标
* 项目构建 profile
* 项目版本
* 项目开发者列表
* 相关邮件列表信息

具体自行百度吧！！！


### Maven Publish Plugin

#### 引入插件

```
plugins {
    id 'maven-publish'
}
```

`maven-publish` 插件使用了一个名为 `publishing` 的扩展，类型是 [PublishingExtension.](https://docs.gradle.org/current/dsl/org.gradle.api.publish.PublishingExtension.html)。

`maven-publish` 插件提供 [MavenPublication](https://docs.gradle.org/current/dsl/org.gradle.api.publish.PublishingExtension.html#org.gradle.api.publish.PublishingExtension:publications(org.gradle.api.Action)) 和 [MavenArtifactRepository](https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.repositories.MavenArtifactRepository.html#org.gradle.api.artifacts.repositories.MavenArtifactRepository:credentials(org.gradle.api.Action)) 一起使用。

#### `MavenPublication` 

`MavenPublication` 是 Gradle 应该如何以 Maven 格式发布内容的表示/配置。 通过提供 MavenPublication 作为类型，可以直接在项目的 publishing.publications 容器中添加一个命名的 Maven Publication。

`MavenPublication` 主要的四项配置

1. 组件
2. 自定义工件
3. 元数据， artifactId, groupId and version
4. POM 文件 


> **组件（由插件提供）**

|   插件  | 组件名称  | 发布内容 | 使用示例 |
|  ----  | ----  | ---- | ---- |
| JavaPlugin  | components.java | jar | from components.java |
| WarPlugin  | components.web | war | from components.war |
| com.android.library | components.variant | aar | from components.release |


**from 函数原型**

```
void from(SoftwareComponent component)
```

> **工件**

我们也可以自己创建一个自定义工件

```
MavenArtifact artifact(Object source)
```

**使用示例**

下面代码来自官网。

```
plugins {
    id 'maven-publish'
}

task sourceJar(type: Jar) {
  archiveClassifier = "sources"
}

publishing {
  publications {
    maven(MavenPublication) {
      artifact sourceJar // Publish the output of the sourceJar task
    }
  }
}
```

下面再通过一个实际的应用场景举例说明。

我们自己的项目引用了一些第三方的库，是通过直接依赖本地jar或者aar的。

```
implementation files('lib/javaparser-core-3.24.2.jar')
```

这个样子的。

现在我们要把自己的的项目发布到仓库，这时我们本地依赖库，是无法添加依赖关系的。所以我们要先把这些本地的jar发布到一个仓库，然后再通过仓库依赖。

```
plugins {
    id 'maven-publish'
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'com.neukol.doc.lib'
      artifactId = 'parser-core'
      version = '1.0.0'

      artifact 'libs/javaparser-core-3.24.2.jar'
    }
  }
}
```

然后我们再依赖

```
implementation 'com.neukol.doc.lib:parser-core:1.0.0'
```

这样我们再通过**组件**的形式把我们自己的本地项目发布到仓库，就可以了。Nice

> **POM 文件**

配置将要发布的 POM，完整内容参考 [MavenPom](https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPom.html)

```
void pom(Action<? super MavenPom> configure)
```

我们看一下 `MavenPom` 就可以知道里面可以做哪些配置了。

```
public interface MavenPom {
    String getPackaging();

    void setPackaging(String var1);

    Property<String> getName();

    Property<String> getDescription();

    Property<String> getUrl();

    Property<String> getInceptionYear();

    void licenses(Action<? super MavenPomLicenseSpec> var1);

    void organization(Action<? super MavenPomOrganization> var1);

    void developers(Action<? super MavenPomDeveloperSpec> var1);

    void contributors(Action<? super MavenPomContributorSpec> var1);

    void scm(Action<? super MavenPomScm> var1);

    void issueManagement(Action<? super MavenPomIssueManagement> var1);

    void ciManagement(Action<? super MavenPomCiManagement> var1);

    void distributionManagement(Action<? super MavenPomDistributionManagement> var1);

    void mailingLists(Action<? super MavenPomMailingListSpec> var1);

    MapProperty<String, String> getProperties();

    void withXml(Action<? super XmlProvider> var1);
}
```

当然我们用的最多的可能就是 `withXml` 做一些依赖传递之类的工作

打印当前项目的所有 `implementation` 依赖

```
pom.withXml {  
    configurations.implementation.allDependencies.each {
        // 避免出现空节点或 artifactId=unspecified 的节点
        if (it.group != null && (it.name != null && "unspecified" != it.name) && it.version != null) {
            println "dependency=${it.toString()}"
        }
    }
}
```

POM 文件中依赖格式

```
  <dependencies>
    <dependency>
      <groupId>junit</groupId>
      <artifactId>junit</artifactId>
      <version>4.12</version>
      <type>jar</type>
      <scope>test</scope>
      <optional>true</optional>
    </dependency>
    ...
  </dependencies>
```

所以添加依赖就要按照这个格式来就行

```
            pom.withXml {

                def dependencies = asNode().appendNode("dependencies") // 关键代码1

                configurations.implementation.allDependencies.each {
                    // 避免出现空节点或 artifactId=unspecified 的节点
                    if (it.group != null && (it.name != null && "unspecified" != it.name) && it.version != null) {
                        def dependency = dependencies.appendNode("dependency") // 关键代码1
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                        println "dependency=${it.toString()}"
                    }
                }
            }
```

**注意！！！**

**注意！！！**

**注意！！！**

通过 from 发布组件是包含了依赖所以不要再做这个依赖传递了。否则会报错

`Duplicated tag: 'dependencies' (position: START_TAG seen ...</dependencies>\n  <dependencies>... @44:17) `

**表示 POM 文件中 dependencies 这个标签重复了**


#### `MavenArtifactRepository` 

`MavenArtifactRepository` 使用 Maven 格式存储工件和元数据的工件存储库。

`MavenArtifactRepository` 主要的三项配置

1. name（可选）
2. URL（必选）
3. credentials（可选）


#### `publishing` 

`publishing` 扩展 包括俩个方法

* `publications` 项目发布产物，搭配 `MavenPublication`

* `repositories` 配置要发布的仓库容器，搭配 `MavenArtifactRepository`




通俗一点来讲，`maven-publish` 这个插件，`publications` 解决了 **what**，`repositories` 解决了 **where**，插件内部要做的事情就是 **how**。

下面我们贴俩个真实的完整的用例。

1. 发布自定义工件

```
plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

publishing {
    // 配置发布产物
    publications {
        maven(MavenPublication) {

            groupId = 'com.neukol.doc.lib'
            artifactId = 'parser-core'
            version = '1.0.0'

            artifact 'libs/javaparser-core-3.24.2.jar'

        }
    }

    // 配置仓库
    repositories {
        maven {
            name = 'localPluginRepository'
            url = "../repo"
        }
    }
}

```

2. 直接发布组件

```
plugins {
    id 'java-gradle-plugin'
    id 'java'
    id 'groovy'
    id 'kotlin'
    id 'maven-publish'
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    implementation 'com.google.guava:guava:31.1-jre'

    implementation 'com.neukol.doc.lib:parser-symbol:1.0.0'
    implementation 'com.neukol.doc.lib:parser-core:1.0.0'
    implementation 'com.google.code.gson:gson:2.9.0'

}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    // 配置发布产物
    publications {
        // 定义一个名字为 docPublish 的发布
        docPublish(MavenPublication) {

            groupId = 'com.doc.plugin.api'
            artifactId = 'neudoc'
            version = '1.0.0'

            from components.java // 使用 Java 插件生成的组件，作为发布的内容


        }
    }

    // 配置仓库
    repositories {
        maven {
            name = 'localPluginRepository'
            url = "../repo"
        }
    }
}

```

参考

[https://docs.gradle.org/current/userguide/userguide.html](https://docs.gradle.org/current/userguide/userguide.html)

[https://developer.android.com/studio/build/maven-publish-plugin?hl=zh-cn](https://developer.android.com/studio/build/maven-publish-plugin?hl=zh-cn)

[https://www.runoob.com/maven/maven-pom.html](https://www.runoob.com/maven/maven-pom.html)

[https://maven.apache.org/pom.html#What_is_the_POM](https://maven.apache.org/pom.html#What_is_the_POM)